<html><!-- Online page at https://registry.terraform.io/providers/aliyun/alicloud/latest/docs/data-sources/route_entries --><head>
                <title>alicloud_route_entries</title>
                <meta charset="utf-8"/>
                <link href="../../../../../../style.css" rel="stylesheet"/>
            </head>
            <body>
                <h1 id="alicloud_route_entries">alicloud_route_entries</h1>

<p>This data source provides a list of Route Entries owned by an Alibaba Cloud account.</p>

<aside class="admonition note">
    <strong>note</strong>
    <em>note</em>
    <p>Available in 1.37.0+.</p>
</aside>

<a class="dashAnchor" name="//apple_ref/cpp/Section/Example%20Usage"></a><h2 id="example-usage">Example Usage</h2>

<pre><code>data "alicloud_zones" "default" {
  available_resource_creation = "VSwitch"
}
data "alicloud_instance_types" "default" {
  availability_zone = "${data.alicloud_zones.default.zones.0.id}"
  cpu_core_count    = 1
  memory_size       = 2
}
data "alicloud_images" "default" {
  name_regex  = "^ubuntu_18.*64"
  most_recent = true
  owners      = "system"
}

variable "name" {
  default = "tf-testAccRouteEntryConfig"
}
resource "alicloud_vpc" "foo" {
  name       = "${var.name}"
  cidr_block = "10.1.0.0/21"
}

resource "alicloud_vswitch" "foo" {
  vpc_id            = "${alicloud_vpc.foo.id}"
  cidr_block        = "10.1.1.0/24"
  availability_zone = "${data.alicloud_zones.default.zones.0.id}"
  vswitch_name      = "${var.name}"
}

resource "alicloud_route_entry" "foo" {
  route_table_id        = "${alicloud_vpc.foo.route_table_id}"
  destination_cidrblock = "172.11.1.1/32"
  nexthop_type          = "Instance"
  nexthop_id            = "${alicloud_instance.foo.id}"
}

resource "alicloud_security_group" "tf_test_foo" {
  name        = "${var.name}"
  description = "foo"
  vpc_id      = "${alicloud_vpc.foo.id}"
}

resource "alicloud_security_group_rule" "ingress" {
  type              = "ingress"
  ip_protocol       = "tcp"
  nic_type          = "intranet"
  policy            = "accept"
  port_range        = "22/22"
  priority          = 1
  security_group_id = "${alicloud_security_group.tf_test_foo.id}"
  cidr_ip           = "0.0.0.0/0"
}

resource "alicloud_instance" "foo" {
  # cn-beijing
  security_groups = ["${alicloud_security_group.tf_test_foo.id}"]

  vswitch_id         = "${alicloud_vswitch.foo.id}"
  allocate_public_ip = true

  # series III
  instance_charge_type       = "PostPaid"
  instance_type              = "${data.alicloud_instance_types.default.instance_types.0.id}"
  internet_charge_type       = "PayByTraffic"
  internet_max_bandwidth_out = 5

  system_disk_category = "cloud_efficiency"
  image_id             = "${data.alicloud_images.default.images.0.id}"
  instance_name        = "${var.name}"
}

data "alicloud_route_entries" "foo" {
  route_table_id = "${alicloud_route_entry.foo.route_table_id}"
}

</code></pre>

<a class="dashAnchor" name="//apple_ref/cpp/Section/Argument%20Reference"></a><h2 id="argument-reference">Argument Reference</h2>

<p>The following arguments are supported:</p>

<ul>
<li><code>route_table_id</code> - (Required, ForceNew) The ID of the router table to which the route entry belongs.</li>
<li><code>instance_id</code> - (Optional) The instance ID of the next hop.</li>
<li><code>type</code> - (Optional) The type of the route entry.</li>
<li><code>cidr_block</code> - (Optional) The destination CIDR block of the route entry.</li>
<li><code>output_file</code> - (Optional) File name where to save data source results (after running <code>terraform plan</code>).</li>
</ul>

<a class="dashAnchor" name="//apple_ref/cpp/Section/Attributes%20Reference"></a><h2 id="attributes-reference">Attributes Reference</h2>

<p>The following attributes are exported in addition to the arguments listed above:</p>

<ul>
<li><code>entries</code> - A list of Route Entries. Each element contains the following attributes:
<ul>
<li><code>type</code> - The type of the route entry.</li>
<li><code>next_hop_type</code> - The type of the next hop.</li>
<li><code>status</code> - The status of the route entry.</li>
<li><code>instance_id</code> - The instance ID of the next hop.</li>
<li><code>route_table_id</code> - The ID of the router table to which the route entry belongs.</li>
<li><code>cidr_block</code> - The destination CIDR block of the route entry.</li>
</ul></li>
</ul>

            
        
    </body></html>