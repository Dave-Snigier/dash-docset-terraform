<html><!-- Online page at https://registry.terraform.io/providers/aliyun/alicloud/latest/docs/data-sources/slb_master_slave_server_groups --><head>
                <title>alicloud_slb_master_slave_server_groups</title>
                <meta charset="utf-8"/>
                <link href="../../../../../../style.css" rel="stylesheet"/>
            </head>
            <body>
                <h1 id="alicloud_slb_master_slave_server_groups">alicloud_slb_master_slave_server_groups</h1>

<p>This data source provides the master slave server groups related to a server load balancer.</p>

<aside class="admonition note">
    <strong>note</strong>
    <em>note</em>
    <p>Available in 1.54.0+</p>
</aside>

<a class="dashAnchor" name="//apple_ref/cpp/Section/Example%20Usage"></a><h2 id="example-usage">Example Usage</h2>

<pre><code>data "alicloud_zones" "default" {
  available_disk_category     = "cloud_efficiency"
  available_resource_creation = "VSwitch"
}

data "alicloud_instance_types" "default" {
  availability_zone = data.alicloud_zones.default.zones[0].id
  eni_amount        = 2
}

data "alicloud_images" "image" {
  name_regex  = "^ubuntu_18.*64"
  most_recent = true
  owners      = "system"
}

variable "name" {
  default = "tf-testAccSlbMasterSlaveServerGroupVpc"
}

variable "number" {
  default = "1"
}

resource "alicloud_vpc" "main" {
  name       = var.name
  cidr_block = "172.16.0.0/16"
}

resource "alicloud_vswitch" "main" {
  vpc_id            = alicloud_vpc.main.id
  zone_id           = data.alicloud_zones.default.zones[0].id
  vswitch_name      = var.name
  cidr_block = "172.16.0.0/16"
}

resource "alicloud_security_group" "group" {
  name   = var.name
  vpc_id = alicloud_vpc.main.id
}

resource "alicloud_instance" "instance" {
  image_id                   = data.alicloud_images.image.images[0].id
  instance_type              = data.alicloud_instance_types.default.instance_types[0].id
  instance_name              = var.name
  count                      = "2"
  security_groups            = [alicloud_security_group.group.id]
  internet_charge_type       = "PayByTraffic"
  internet_max_bandwidth_out = "10"
  availability_zone          = data.alicloud_zones.default.zones[0].id
  instance_charge_type       = "PostPaid"
  system_disk_category       = "cloud_efficiency"
  vswitch_id                 = alicloud_vswitch.main.id
}

resource "alicloud_slb_load_balancer" "instance" {
  load_balancer_name = var.name
  vswitch_id    = alicloud_vswitch.main.id
  load_balancer_spec = "slb.s2.small"
}

resource "alicloud_slb_master_slave_server_group" "group" {
  load_balancer_id = alicloud_slb_load_balancer.instance.id
  name             = var.name

  servers {
    server_id   = alicloud_instance.instance[0].id
    port        = 100
    weight      = 100
    server_type = "Master"
  }

  servers {
    server_id   = alicloud_instance.instance[1].id
    port        = 100
    weight      = 100
    server_type = "Slave"
  }
}

data "alicloud_slb_master_slave_server_groups" "sample_ds" {
  load_balancer_id = alicloud_slb_load_balancer.instance.id
}

output "first_slb_server_group_id" {
  value = data.alicloud_slb_master_slave_server_groups.sample_ds.groups[0].id
}
</code></pre>

<a class="dashAnchor" name="//apple_ref/cpp/Section/Argument%20Reference"></a><h2 id="argument-reference">Argument Reference</h2>

<p>The following arguments are supported:</p>

<ul>
<li><code>load_balancer_id</code> - ID of the SLB.</li>
<li><code>ids</code> - (Optional) A list of master slave server group IDs to filter results.</li>
<li><code>name_regex</code> - (Optional) A regex string to filter results by master slave server group name.</li>
<li><code>output_file</code> - (Optional) File name where to save data source results (after running <code>terraform plan</code>).</li>
</ul>

<a class="dashAnchor" name="//apple_ref/cpp/Section/Attributes%20Reference"></a><h2 id="attributes-reference">Attributes Reference</h2>

<p>The following attributes are exported in addition to the arguments listed above:</p>

<ul>
<li><code>ids</code> - A list of SLB master slave server groups IDs.</li>
<li><code>names</code> - A list of SLB master slave server groups names.</li>
<li><code>groups</code> - A list of SLB master slave server groups. Each element contains the following attributes:
<ul>
<li><code>id</code> - master slave server group ID.</li>
<li><code>name</code> - master slave server group name.</li>
<li><code>servers</code> - ECS instances associated to the group. Each element contains the following attributes:</li>
<li><code>instance_id</code> - ID of the attached ECS instance.</li>
<li><code>weight</code> - Weight associated to the ECS instance.</li>
<li><code>port</code> - The port used by the master slave server group.</li>
<li><code>server_type</code> - The server type of the attached ECS instance.</li>
<li><code>is_backup</code> - (Removed from v1.63.0) Determine if the server is executing.</li>
</ul></li>
</ul>

            
        
    </body></html>